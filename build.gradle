plugins {
  id 'org.springframework.boot' version '2.2.5.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'org.asciidoctor.convert' version '1.5.8'
  id 'java'
}

allprojects {
  apply from: rootProject.file('versions.gradle')

  tasks.withType(JavaCompile) {
    sourceCompatibility = 13
    targetCompatibility = 13
    options.encoding = 'UTF-8'
  }

  repositories {
    jcenter()
  }
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'io.spring.dependency-management'

  dependencyManagement {
    imports {
      mavenBom("org.springframework.boot:spring-boot-dependencies:${versions['spring-boot']}")
    }
  }

  sourceSets {
    main {
      java {
        srcDirs = ['src/main/java']
      }
      resources {
        srcDirs = ['src/main/resources']
      }
    }
    test {
      java {
        srcDirs = ['src/test/java']
      }
      resources {
        srcDirs = ['src/test/resources']
      }
    }
  }

  javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options {
      charSet 'UTF-8'
      encoding 'UTF-8'
      docEncoding 'UTF-8'
      failOnError false
      linkSource true
    }
  }

  configurations {
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
    compileOnly {
      extendsFrom annotationProcessor
    }
  }

  dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: versions['jetbrains']

    implementation group: 'org.mapstruct', name: 'mapstruct', version: versions['mapstruct']
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions['mapstruct']

    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
  }
}
